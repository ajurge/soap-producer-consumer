ext.jaxwsSourceDir = "${buildDir}/generated/sources/jaxws"

configurations {
  jaxws
}

task wsimport {
  description = 'Generate classes from wsdl using wsimport'

  doLast {
    project.mkdir(jaxwsSourceDir)
    ant {
      taskdef(name: 'wsimport',
        classname: 'com.sun.tools.ws.ant.WsImport',
        classpath: configurations.jaxws.asPath
      )
      wsimport(
        keep: true,
        destdir: jaxwsSourceDir,
        extension: "true",
        verbose: true,
        wsdl: "http://localhost:8081/ws/countries.wsdl",
        xnocompile: true,
        package: "com.inion.consumingwebservice.wsdl") {
        xjcarg(value: "-XautoNameResolution")
      }
    }
  }
}

sourceSets {
  main {
    java.srcDirs += jaxwsSourceDir
  }
}

compileJava {
  dependsOn wsimport
}
// end::wsdl[]

dependencies {
  implementation ('org.springframework.boot:spring-boot-starter-web-services') {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
  }
  jaxws 'com.sun.xml.ws:jaxws-tools:3.0.0',
    'jakarta.xml.ws:jakarta.xml.ws-api:3.0.0',
    'jakarta.xml.bind:jakarta.xml.bind-api:3.0.0',
    'jakarta.activation:jakarta.activation-api:2.0.0',
    'com.sun.xml.ws:jaxws-rt:3.0.0'
}
